# Maintainer: Markus Wamser <github.com/wamserma>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=cabal-install
pkgver=1.22.6.0
pkgrel=1
pkgdesc="The command-line interface for Cabal and Hackage."
url="http://hackage.haskell.org/package/cabal-install"
license=('custom:BSD3')
arch=('armv7h')
depends=('gmp' 'zlib' 'libffi')
makedepends=('ghc' 'chrpath')
source=("https://www.haskell.org/cabal/release/cabal-install-${pkgver}/cabal-install-${pkgver}.tar.gz")
md5sums=('0463650044697ea337705ea9e29a0221')

# using the bootstrap script, while official arch package has circular dependency

build() {
  cd cabal-install-${pkgver}

  ./bootstrap.sh --sandbox
}

package() {
  cd cabal-install-${pkgver}

  install -d ${pkgdir}/usr/bin
  install -m755 dist/build/cabal/cabal \
    ${pkgdir}/usr/bin

  # remove insecure rpath
  chrpath -d ${pkgdir}/usr/bin/cabal

  install -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 LICENSE \
    ${pkgdir}/usr/share/licenses/${pkgname}

  # add bash completion
  install -d ${pkgdir}/usr/share/bash-completion/completions
  install -m644 bash-completion/cabal \
    ${pkgdir}/usr/share/bash-completion/completions
}
